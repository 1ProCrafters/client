import com.github.glassmc.kiln.*
import com.github.glassmc.kiln.environment.*

buildscript {
	repositories {
		mavenCentral()
		mavenLocal()
		maven { url "https://glassmc.ml/repository/" }
		maven { url "https://jitpack.io/" }
		maven { url "https://libraries.minecraft.net/" }
	}

	dependencies {
		// local build of kiln f76eab4c
		classpath "com.github.glassmc:kiln:0.9.3-SNAPSHOT"
	}
}

plugins {
	id "io.freefair.lombok" version "6.4.3" apply false
}

class SolClientKilnEnvironment implements Environment {

	@Override
	String getMainClass() {
		return "io.github.solclient.client.wrapper.BootstrapMain"
	}

	@Override
	String[] getProgramArguments(String environment, String version) {
		return new String[] { "--bootstrap:minecraftVersion", version, "--bootstrap:dev", "true" }
	}

	@Override
	String[] getRuntimeDependencies(File file) {
		return new String[0]
	}

	@Override
	String getVersion(String mcVersion) {
		return "Sol-Client-" + mcVersion
	}

}

allprojects {
	apply plugin: "kiln"
	apply plugin: "java"
	apply plugin: "io.freefair.lombok"

	repositories {
		mavenCentral()
		maven { url "https://libraries.minecraft.net" }
		maven { url "https://repo.spongepowered.org/maven" }
		maven { url "https://repo.codemc.io/repository/maven-public" }
		maven { url "https://repo.hypixel.net/repository/Hypixel" }
		maven { url "https://jitpack.io" }
		maven { url "https://glassmc.ml/repository/" }
	}
	
	kiln {
		environment = new SolClientKilnEnvironment()
		transformers = [ new MixinRemapper() ]
	}
}

subprojects {
	
	dependencies {
		implementation(project(":")) {
			for(def subproject : rootProject.subprojects) {
				exclude module: subproject.name
			}
			exclude group: "org.lwjgl"
		}
	}
	
}

dependencies {
	// minimal lwjgl, simply to compile against since the version can vary
	compileOnly "org.lwjgl:lwjgl-opengl:3.3.1"

	shadowImplementation files(rootProject.projectDir.getPath() + "/libs/occlusionculling.jar")

	api "net.hypixel:hypixel-api-core:4.0"
	api "com.github.JnCrMx:discord-game-sdk4j:v0.5.4"
	api "org.java-websocket:Java-WebSocket:1.5.3"
	api "org.apache.httpcomponents:httpclient:4.5.13"

	api "org.spongepowered:mixin:0.8.5"
	api "org.ow2.asm:asm-tree:9.3"
	api "org.ow2.asm:asm-commons:9.3"
	api "org.ow2.asm:asm-util:9.3"

	compileOnly "org.apache.logging.log4j:log4j-api:2.17.2"
	compileOnly "org.apache.logging.log4j:log4j-core:2.17.2"
	compileOnly "commons-io:commons-io:2.7"
	compileOnly "org.apache.commons:commons-lang3:3.12.0"
	compileOnly "com.google.code.findbugs:jsr305:3.0.2"
	compileOnly "com.google.guava:guava:31.1-jre"
	
	api "org.jetbrains:annotations:23.0.0"
}
