import net.fabricmc.loom.LoomGradleExtension

plugins {
	id "fabric-loom" version "0.12-SNAPSHOT" apply false
	id "io.freefair.lombok" version "6.4.3" apply false
	id "com.github.johnrengelman.shadow" version "7.1.2" apply false
}

allprojects {
	apply plugin: "java"
	apply plugin: "java-library"
	apply plugin: "com.github.johnrengelman.shadow"
	apply plugin: "io.freefair.lombok"

	repositories {
		mavenCentral()
		maven { url "https://libraries.minecraft.net" }
		maven { url "https://repo.spongepowered.org/maven" }
		maven { url "https://repo.codemc.io/repository/maven-public" }
		maven { url "https://repo.hypixel.net/repository/Hypixel" }
		maven { url "https://jitpack.io" }
		maven { url "https://maven.legacyfabric.net" }
	}
}

subprojects {
	apply plugin: "fabric-loom"
	
	dependencies {
		implementation project(":")
		annotationProcessor "org.ow2.asm:asm:${project.asmVersion}"
		annotationProcessor "org.ow2.asm:asm-analysis:${project.asmVersion}"
		annotationProcessor "org.ow2.asm:asm-commons:${project.asmVersion}"
		annotationProcessor "org.ow2.asm:asm-tree:${project.asmVersion}"
		annotationProcessor "org.ow2.asm:asm-util:${project.asmVersion}"
	}

	for (runConfig in LoomGradleExtension.get(project).getRunConfigs()) {
		runConfig.defaultMainClass "io.github.solclient.client.wrapper.BootstrapMain"
	}
}

dependencies {
	// minimal lwjgl, simply to compile against since the version can vary
	compileOnly "org.lwjgl:lwjgl-opengl:3.3.1"

	api files(rootProject.projectDir.getPath() + "/libs/occlusionculling.jar")

	api "net.hypixel:hypixel-api-core:4.0"
	api "com.github.JnCrMx:discord-game-sdk4j:v0.5.4"
	api "org.java-websocket:Java-WebSocket:1.5.3"
	api "org.apache.httpcomponents:httpclient:4.5.13"

	api "org.spongepowered:mixin:0.8.5"
	api "org.ow2.asm:asm-tree:${project.asmVersion}"
	api "org.ow2.asm:asm-commons:${project.asmVersion}"
	api "org.ow2.asm:asm-util:${project.asmVersion}"

	compileOnly "org.apache.logging.log4j:log4j-api:${project.log4jVersion}"
	compileOnly "org.apache.logging.log4j:log4j-core:${project.log4jVersion}"
	compileOnly "commons-io:commons-io:2.7"
	compileOnly "org.apache.commons:commons-lang3:3.12.0"
	compileOnly "com.google.code.findbugs:jsr305:3.0.2"
	compileOnly "com.google.guava:guava:31.1-jre"
	
	api "org.jetbrains:annotations:23.0.0"
}
